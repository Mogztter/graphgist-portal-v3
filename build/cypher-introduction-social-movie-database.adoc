= Cypher Introduction - Social Movie Database

:neo4j-version: 3.5

== Domain model and setup

Our example graph consists of movies with an id, year and title and actors with a name. Actors have an +ACTS_IN+ relationship to movies, which represents the  role they played, the role relationship has also a role attribute.

image::http://assets.neo4j.org/img/still/cineasts.gif[]

We encourage you to enter the Cypher statements in the interactive console manually, but you can also click on the queries to run them. 

Like this one

[source,cypher]
----
MATCH (n)
RETURN "Hello Graph with "+count(*)+" Nodes!" 
as welcome;
----

//setup
//hide
[source,cypher]
----
create (matrix1:Movie {id : '603', title : 'The Matrix', year : '1999-03-31'}),
 (matrix2:Movie {id : '604', title : 'The Matrix Reloaded', year : '2003-05-07'}),
 (matrix3:Movie {id : '605', title : 'The Matrix Revolutions', year : '2003-10-27'}),

 (neo:Actor {name:'Keanu Reeves'}),
 (morpheus:Actor {name:'Laurence Fishburne'}),
 (trinity:Actor {name:'Carrie-Anne Moss'}),

 (matrix1)<-[:ACTS_IN {role : 'Neo'}]-(neo),
 (matrix2)<-[:ACTS_IN {role : 'Neo'}]-(neo),
 (matrix3)<-[:ACTS_IN {role : 'Neo'}]-(neo),
 (matrix1)<-[:ACTS_IN {role : 'Morpheus'}]-(morpheus),
 (matrix2)<-[:ACTS_IN {role : 'Morpheus'}]-(morpheus),
 (matrix3)<-[:ACTS_IN {role : 'Morpheus'}]-(morpheus),
 (matrix1)<-[:ACTS_IN {role : 'Trinity'}]-(trinity),
 (matrix2)<-[:ACTS_IN {role : 'Trinity'}]-(trinity),
 (matrix3)<-[:ACTS_IN {role : 'Trinity'}]-(trinity)

----

//console

== First Steps with Cypher

return a single node, by id (The Matrix) 

[source,cypher]
----
MATCH (movie:Movie {title:"The Matrix"})
RETURN movie;
----

return the title and id of the matrix node 

[source,cypher]
----
MATCH (movie:Movie {title:"The Matrix"})
RETURN movie.id, movie.title;
----

//table

first use of the MATCH clause, show all actors 

[source,cypher]
----
MATCH (m:Movie {title:"The Matrix"})<-[:ACTS_IN]-(actor)
RETURN actor;
----
return just the name, order them by name 

[source,cypher]
----
MATCH (m:Movie {title:"The Matrix"})<-[:ACTS_IN]-(actor)
RETURN actor.name order by actor.name;
----

first aggregation, count the actors 

[source,cypher]
----
MATCH (m:Movie {title:"The Matrix"})<-[:ACTS_IN]-(actor)
RETURN count(*);
----

first filtering only the actors whose names end with "s" 

[source,cypher]
----
MATCH (m:Movie {title:"The Matrix"})<-[:ACTS_IN]-(actor)
WHERE actor.name =~ ".*s$" 
RETURN actor.name;
----

== Interlude: Explore a dataset

some exploratory queries for unknown datasets, don't do that on live production instances

Count nodes 

[source,cypher]
----
MATCH (n) 
RETURN count(*);
----

Count relationship types 

[source,cypher]
----
MATCH (n)-[r]->()
RETURN type(r), count(*);
----

//table

List all nodes and their relationships

[source,cypher]
----
MATCH (n)-[r]->(m)
RETURN n as from, r as `->`, m as to;
----

//table

Next up is link:./?dropbox-14493611%2Fcypher-introduction-update.adoc[Updating the Graph]
