= Asturias Highway network
Daniel Lebredo and Jose Enrique Álvarez
v.1.5
:twitter: @DaniLebredo
:tags: Universidad de Oviedo, Neo4j, Repositorios de Informacion
:neo4j-version: 3.5

// hide
// setup
[source,cypher]
----
CREATE
(Oviedo:City { name:'Oviedo', population: 225973 }),
(Gijon:City { name:'Gijon', population: 277733 }),
(Aviles:Village { name:'Avilés', population: 83107 }),
(Grado:Village { name:'Grado', population: 10746 }),
(Siero:Village { name:'Siero', population: 12615 }),
(Mieres:Village { name:'Mieres' , population:42421 }),
(Lena:Village { name:'Lena', population: 12367 }),
(Campomanes:Town { name:'Campomanes', population: 739 }),
(Nava:Town { name:'Nava', population: 5599 }),
(Infiesto:Town { name:'Infiesto', population: 2139 }),
(Arriondas:Town { name:'Arriondas', population: 2575 }),
(Ribadesella:Town { name:'Ribadesella', population: 6209 }),
(Villaviciosa:Village { name:'Villaviciosa', population: 14989 }),
(Colunga:Village { name:'Colunga', population: 3614 }),
(Salas:Village { name:'Salas', population: 5659 }),
(Tineo:Town { name:'Tineo', population: 10652 }),
(CangasDelNarcea:Town { name:'Cangas del Narcea', population: 14077 }),
(Allande:Village { name:'Allande', population: 1939 }),
(Luarca:Village { name:'Luarca', population: 5292 }),
(Cudillero:Village { name:'Cudillero', population: 5691 }),
(Navia:Village { name:'Navia', population: 8982 }),
(Ribadeo:Town { name:'Ribadeo', population: 10061 }),
(Somiedo:Village { name:'Pola de Somiedo', population: 242 }),
(Laviana:Village { name:'Pola de Laviana', population: 9106 }),
(Ribadeo)<-[:NATIONAL_ROAD { name:'N-634' , length:33 , vehiclesPerDay:5144  }]-(Navia),
(Ribadeo)<-[:MOTORWAY { name:'A-8' , length:30, vehiclesPerDay:8656   }]-(Navia),
(Luarca)-[:NATIONAL_ROAD { name:'N-634' , length:21 , vehiclesPerDay:11796  }]->(Navia),
(Luarca)-[:REGIONAL_ROAD { name:'AS-219' , length:60  }]->(Allande),
(Allande)-[:REGIONAL_ROAD { name:'AS-217' , length:35  }]->(Tineo),
(Allande)-[:REGIONAL_ROAD { name:'AS-14' , length:20  }]->(CangasDelNarcea),
(Tineo)-[:REGIONAL_ROAD { name:'AS-15' , length:30 , vehiclesPerDay:3403  }]->(CangasDelNarcea),
(Salas)-[:REGIONAL_ROAD { name:'AS-216' , length:23  }]->(Tineo),
(Salas)-[:NATIONAL_ROAD { name:'N-634' , length:51 , vehiclesPerDay:383  }]->(Luarca),
(Grado)-[:NATIONAL_ROAD { name:'N-634' , length:22 , vehiclesPerDay:4334  }]->(Salas),
(Oviedo)-[:NATIONAL_ROAD { name:'N-634' , length:27  , vehiclesPerDay:2157 }]->(Grado),
(Oviedo)-[:HIGHWAY { name:'A-63' , length:26  , vehiclesPerDay:11163 }]->(Grado),
(Oviedo)-[:HIGHWAY { name:'A-66' , length:35, vehiclesPerDay:57324  }]->(Aviles),
(Aviles)-[:NATIONAL_ROAD { name:'N-632' , length:26  }]->(Cudillero),
(Aviles)-[:MOTORWAY { name:'A-8' , length:27  }]->(Cudillero),
(Cudillero)-[:MOTORWAY { name:'A-8' , length:36 , vehiclesPerDay:11755   }]->(Luarca),
(Oviedo)-[:HIGHWAY { name:'A-66' , length:34 , vehiclesPerDay:60153 }]->(Gijon),
(Oviedo)-[:HIGHWAY { name:'AS-II' , length:31 , vehiclesPerDay:21293 }]->(Gijon),
(Oviedo)-[:HIGHWAY { name:'A-66' , length:19  , vehiclesPerDay:50000 }]->(Mieres),
(Oviedo)-[:HIGHWAY { name:'A-64' , length:19 , vehiclesPerDay:50000  }]->(Siero),
(Mieres)-[:HIGHWAY { name:'AS-I' , length:38 , vehiclesPerDay:21608 }]->(Gijon),
(Mieres)-[:HIGHWAY { name:'A-66' , length:13  , vehiclesPerDay:28848 }]->(Lena),
(Lena)-[:HIGHWAY { name:'A-66' , length:8 , vehiclesPerDay:11772  }]->(Campomanes),
(Siero)-[:NATIONAL_ROAD { name:'N-634' , length:14 , vehiclesPerDay:11201   }]->(Nava),
(Nava)-[:NATIONAL_ROAD { name:'N-634' , length:14 , vehiclesPerDay:6508  }]->(Infiesto),
(Infiesto)-[:NATIONAL_ROAD { name:'N-634' , length:21 , vehiclesPerDay:6780  }]->(Arriondas),
(Arriondas)-[:NATIONAL_ROAD { name:'N-634' , length:18 , vehiclesPerDay:5368 }]->(Ribadesella),
(Gijon)-[:NATIONAL_ROAD { name:'N-632' , length:32  , vehiclesPerDay:388}]->(Villaviciosa),
(Gijon)-[:MOTORWAY { name:'A-8' , length:27, vehiclesPerDay:22358  }]->(Villaviciosa),
(Villaviciosa)-[:NATIONAL_ROAD { name:'N-632' , length:18 , vehiclesPerDay:391  }]->(Colunga),
(Villaviciosa)-[:MOTORWAY { name:'A-8' , length:21  , vehiclesPerDay:15427}]->(Colunga),
(Colunga)-[:NATIONAL_ROAD { name:'N-632' , length:25  , vehiclesPerDay:989}]->(Ribadesella),
(Colunga)-[:MOTORWAY{ name:'A-8' , length:22  , vehiclesPerDay:12205}]->(Ribadesella),
(Infiesto)-[:REGIONAL_ROAD { name:'AS-255' , length:21  }]->(Villaviciosa),
(Arriondas)-[:REGIONAL_ROAD { name:'AS-260' , length:21  }]->(Colunga),
(Siero)-[:HIGHWAY { name:'A-64' , length:26  }]->(Villaviciosa),
(CangasDelNarcea)-[:REGIONAL_ROAD { name:'AS-15/AS-227' , length:91  }]->(Somiedo),
(Somiedo)-[:REGIONAL_ROAD { name:'AS-228/AS-229' , length:71  }]->(Lena),
(Mieres)-[:REGIONAL_ROAD { name:'AS-112/AS-252' , length:55  }]->(Laviana),
(Laviana)-[:REGIONAL_ROAD { name:'AS-251' , length:25  }]->(Nava),
(Grado)-[:REGIONAL_ROAD { name:'AS-237' , length:29  }]->(Aviles),
(Somiedo)-[:REGIONAL_ROAD { name:'AS-227' , length:55  }]->(Grado)
----

=== Domain

Asturias is a northern province of Spain, known for its harsh geography and its poor communications. Over the course of the years, Asturias' ground communications have slowly advanced from old national roads to brand new highways that connect the most important cities of the region. i.e.: Oviedo, the capital city; Gijón, the most populated, and the runner-ups of Avilés, Mieres and Villaviciosa.

//image:https://dl.dropboxusercontent.com/u/5488524/AsturiasHighwayNetworkGraph.jpg[]

=== Queries

==== Basic queries

*National road section with highest vehicle transit*

[source,cypher]
----
MATCH (n)-[r:NATIONAL_ROAD]->(m)
WHERE r.vehiclesPerDay is not NULL
RETURN n.name, m.name, r.vehiclesPerDay, r.length
ORDER BY r.vehiclesPerDay DESC
LIMIT 1
----
//table

*Closest town to Oviedo*

[source,cypher]
----
MATCH (n)-[r]-(m)
WHERE n.name="Oviedo"
RETURN m.name, r.length
ORDER BY r.length asc
LIMIT 1
----
//table

=== Intermediate queries

*Cities connected by highway with Mieres*

In order to know this connectivity it is required to gather the places reachable by highway from Mieres.

[source,cypher]
----
MATCH (from)-[:HIGHWAY*]->(to)
WHERE from.name = 'Mieres'
return to.name
----
//table

*Routes from Navia to Colunga*

We want to get three routes connecting Navia, a pretty western town, to Colunga, a beautiful eastern town.

[source,cypher]
----
MATCH ruta=(navia {name:"Navia"})-[*6..8]-(colunga {name:"Colunga"})
RETURN [city IN nodes(ruta)| city.name] as route
LIMIT 3
----
//table

=== Advanced queries

*Minimum path from Mieres to Infiesto*

We seek the minimum path from Mieres to Infiesto. The minimum path is the one that traverses the least amount of towns.

[source,cypher]
----
MATCH p= shortestPath((a)-[*]-(b))
WHERE a.name='Mieres' and b.name='Infiesto'
RETURN [city IN nodes(p)| city.name] as path
----
// table

*Minimum distance from Oviedo to Luarca*

We want to retrieve the minimum distance from Oviedo to Luarca and get the associated path.

[source,cypher]
----
match recorrido=(oviedo {name:"Oviedo"})-[*1..3]-(luarca {name:"Luarca"})
return [x IN nodes(recorrido)| x.name] as path, reduce(long = 0, via in relationships(recorrido) | long + via.length) as distance
order by reduce(long = 0, via in relationships(recorrido) | long + via.length)
limit 1
----
// table
